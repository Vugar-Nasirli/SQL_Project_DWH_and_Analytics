-- Data Transformations on Bronze Layer Data and Load into Silver Layer
USE DataWarehouse;
GO

-- Stored Procedure for FULL LOAD (TRUNCATE & INSERT) all tables
CREATE OR ALTER PROCEDURE silver.load_silver
AS
BEGIN
	DECLARE @start_time DATETIME, @end_time DATETIME, @batch_start_time DATETIME, @batch_end_time DATETIME;
	SET @batch_start_time = GETDATE();
	BEGIN TRY
		PRINT('=============================');
		PRINT('Loading Silver Layer...');
		PRINT('=============================');
		PRINT('');
		PRINT('-----------------------------');
		PRINT('Loading CRM Tables...');
		PRINT('-----------------------------');

		-- Table: silver.crm_cust_info

		SET @start_time = GETDATE();
		-- Truncate
		PRINT('');
		PRINT('>> Truncating Table: silver.crm_cust_info');
		TRUNCATE TABLE silver.crm_cust_info;

		-- Insert
		PRINT('');
		PRINT('>> Inserting Data Into: silver.crm_cust_info');
		INSERT INTO silver.crm_cust_info
		(
			cst_id,
			cst_key,
			cst_firstname,
			cst_lastname,
			cst_marital_status,
			cst_gndr,
			cst_create_date
		)
		SELECT
			cst_id,
			cst_key,
			TRIM(cst_firstname) AS cst_firstname,
			TRIM(cst_lastname) AS cst_lastname,
			CASE UPPER(TRIM(cst_marital_status))
				WHEN 'S' THEN 'Single'
				WHEN 'M' THEN 'Married'
				ELSE 'n/a'
			END AS cst_marial_status,
			CASE UPPER(TRIM(cst_gndr))
				WHEN 'F' THEN 'Female'
				WHEN 'M' THEN 'Male'
				ELSE 'n/a'
			END AS cst_gndr,
			cst_create_date
		FROM
		(
			SELECT
				ROW_NUMBER() OVER(
					PARTITION BY cst_id
					ORDER BY cst_create_date DESC
				) AS flag,
				*
			FROM
				bronze.crm_cust_info
			WHERE
				cst_id IS NOT NULL
		) AS T
		WHERE
			flag = 1;

		SET @end_time = GETDATE();
		PRINT('');
		PRINT('>> Load Duration: ' + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR) + ' seconds');
		PRINT('-----------------------------');
		------------------------------------------------

		-- Table: silver.crm_prd_info

		SET @start_time = GETDATE();
		-- Truncate
		PRINT('');
		PRINT('>> Truncating Table: silver.crm_prd_info');
		TRUNCATE TABLE silver.crm_prd_info;

		-- Insert
		PRINT('');
		PRINT('>> Inserting Data Into: silver.crm_prd_info');
		INSERT INTO silver.crm_prd_info
		(
			prd_id,
			cat_id,
			prd_key,
			prd_nm,
			prd_cost,
			prd_line,
			prd_start_dt,
			prd_end_dt
		)
		SELECT
			prd_id,
			REPLACE(SUBSTRING(prd_key, 1, 5),'-', '_') AS cat_id,
			SUBSTRING(prd_key, 7, LEN(prd_key)) AS prd_key,
			prd_nm,
			ISNULL(prd_cost, 0) AS prd_cost,
			CASE UPPER(TRIM(prd_line))
				WHEN 'M' THEN 'Mountain'
				WHEN 'R' THEN 'Road'
				WHEN 'S' THEN 'Other Sales'
				WHEN 'T' THEN 'Touring'
				ELSE 'n/a'
			END AS prd_line,
			CAST(prd_start_dt AS DATE) AS prd_start_dt,
			CAST(DATEADD(DAY, -1, LEAD(prd_start_dt) OVER(
					PARTITION BY prd_key
					ORDER BY prd_start_dt
				)) AS DATE) AS prd_end_dt
		FROM
			bronze.crm_prd_info;

		SET @end_time = GETDATE();
		PRINT('');
		PRINT('>> Load Duration: ' + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR) + ' seconds');
		PRINT('-----------------------------');
		------------------------------------------------------------

		-- Table: crm_sales_details

		SET @start_time = GETDATE();
		-- Truncate
		PRINT('');
		PRINT('>> Truncating Table: silver.crm_sales_details');
		TRUNCATE TABLE silver.crm_sales_details;

		-- Insert
		PRINT('');
		PRINT('>> Inseerting Data Into: silver.crm_sales_details');
		INSERT INTO silver.crm_sales_details
		(
			sls_ord_num,
			sls_prd_key,
			sls_cust_id,
			sls_order_dt,
			sls_ship_dt,
			sls_due_dt,
			sls_sales,
			sls_quantity,
			sls_price
		)
		SELECT
			sls_ord_num,
			sls_prd_key,
			sls_cust_id,
			CASE
				WHEN sls_order_dt <= 0 OR LEN(sls_order_dt) != 8 THEN NULL
				ELSE CAST(CAST(sls_order_dt AS VARCHAR) AS DATE)
			END AS sls_order_dt,
			CASE
				WHEN sls_ship_dt <= 0 OR LEN(sls_ship_dt) != 8 THEN NULL
				ELSE CAST(CAST(sls_ship_dt AS VARCHAR) AS DATE)
			END AS sls_ship_dt,
			CASE
				WHEN sls_due_dt <= 0 OR LEN(sls_due_dt) != 8 THEN NULL
				ELSE CAST(CAST(sls_due_dt AS VARCHAR) AS DATE)
			END AS sls_due_dt,
			CASE       
				WHEN (sls_sales IS NULL OR sls_sales = 0) OR sls_sales != sls_quantity * ABS(sls_price) THEN sls_quantity * ABS(sls_price)
				ELSE ABS(sls_sales)
			END AS sls_sales,
			sls_quantity,
			CASE       
				WHEN sls_price IS NULL OR sls_price = 0 THEN ABS(sls_sales) / NULLIF(sls_quantity, 0)
				ELSE ABS(sls_price)
			END AS sls_price
		FROM
			bronze.crm_sales_details;

		SET @end_time = GETDATE();
		PRINT('');
		PRINT('>> Load Duration: ' + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR) + ' seconds');
		PRINT('-----------------------------');
		------------------------------------------------------------------
		------------------------------------------------------------------
		PRINT('');
		PRINT('-----------------------------');
		PRINT('Loading ERP Tables...');
		PRINT('-----------------------------');

		-- Table: erp_cust_az12

		SET @start_time = GETDATE();
		-- Truncate
		PRINT('');
		PRINT('>> Truncating Table: silver.erp_cust_az12');
		TRUNCATE TABLE silver.erp_cust_az12;

		-- Insert
		PRINT('');
		PRINT('>> Inserting Data Into: silver.erp_cust_az12');
		INSERT INTO silver.erp_cust_az12
		(
			cid,
			bdate,
			gen
		)
		SELECT
			SUBSTRING(cid, LEN(cid) - 9, LEN(cid)) AS cid,
			CASE
				WHEN bdate > DATEADD(YEAR, -18, GETDATE()) THEN NULL
				ELSE bdate
			END AS bdate,
			CASE 
				WHEN TRIM(gen) IS NULL OR TRIM(gen) = '' THEN 'n/a'
				WHEN TRIM(gen) = 'F' THEN 'Female'
				WHEN TRIM(gen) = 'M' THEN 'Male'
				ELSE TRIM(gen)
			END gen
		FROM
			bronze.erp_cust_az12;

		SET @end_time = GETDATE();
		PRINT('');
		PRINT('>> Load Duration: ' + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR) + ' seconds');
		PRINT('-----------------------------');
		-------------------------------------------------------------------------

		-- Table: erp_loc_a101

		SET @start_time = GETDATE();
		-- Truncate
		PRINT('');
		PRINT('>> Truncating Table: silver.erp_loc_a101');
		TRUNCATE TABLE silver.erp_loc_a101;

		-- Insert
		PRINT('');
		PRINT('>> Inserting Data Into: silver.erp_loc_a101');
		INSERT INTO silver.erp_loc_a101
		(
			cid,
			cntry
		)
		SELECT
			REPLACE(cid, '-', '') AS cid,
			CASE
				WHEN UPPER(TRIM(cntry)) IN ('USA','US','UNITED STATES') THEN 'USA'
				WHEN UPPER(TRIM(cntry)) IN ('DE','GERMANY') THEN 'Germany'
				WHEN UPPER(TRIM(cntry)) = '' OR cntry IS NULL THEN 'n/a'
				ELSE TRIM(cntry)
			END AS cntry
		FROM
			bronze.erp_loc_a101;

		SET @end_time = GETDATE();
		PRINT('');
		PRINT('>> Load Duration: ' + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR) + ' seconds');
		PRINT('-----------------------------');
		-------------------------------------------------------------------------

		-- Table: erp_px_cat_g1v2

		SET @start_time = GETDATE();
		-- Truncate
		PRINT('');
		PRINT('>> Truncating Table: silver.erp_px_cat_g1v2');
		TRUNCATE TABLE silver.erp_px_cat_g1v2;

		-- Insert
		PRINT('');
		PRINT('>> Inserting Data Into: silver.erp_px_cat_g1v2');
		INSERT INTO silver.erp_px_cat_g1v2
		(
			id,
			cat,
			subcat,
			maintenance
		)
		SELECT
			id,
			cat,
			subcat,
			maintenance
		FROM
			bronze.erp_px_cat_g1v2;

		SET @end_time = GETDATE();
		PRINT('');
		PRINT('>> Load Duration: ' + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR) + ' seconds');
		PRINT('-----------------------------');
	END TRY
	BEGIN CATCH
		PRINT('===================================================');
		PRINT('ERROR OCCURED DURING LOADING BRONZE LAYER!');
		PRINT('Error Message: ' + ERROR_MESSAGE());
		PRINT('Error Number: ' + CAST(ERROR_NUMBER() AS NVARCHAR));
		PRINT('Error State: ' + CAST(ERROR_STATE() AS NVARCHAR));
		PRINT('Error Line: ' + CAST(ERROR_LINE() AS NVARCHAR));
		PRINT('===================================================');
	END CATCH

	SET @batch_end_time = GETDATE();
	PRINT('');
	PRINT('==============================================');
	PRINT('Loading Bronze Layer Is Completed!');
	PRINT('Total Load Duration: ' + CAST(DATEDIFF(SECOND, @batch_start_time, @batch_end_time) AS NVARCHAR) + ' seconds');
	PRINT('==============================================');
END;